# # Particle Set
#
#md # [![](https://mybinder.org/badge_logo.svg)](@__BINDER_ROOT_URL__/notebooks/random_flow_field.ipynb)
#md # [![](https://img.shields.io/badge/show-nbviewer-579ACA.svg)](@__NBVIEWER_ROOT_URL__/notebooks/random_flow_field.ipynb)
#
# Simulate trajectories of a particle cloud in a two-dimensional flow field.
# A doubly-periodic domain and randomly-generated flow fields are initially used.
# For additional documentation e.g. see :
# [1](https://JuliaClimate.github.io/IndividualDisplacements.jl/dev/),
# [2](https://JuliaClimate.github.io/MeshArrays.jl/dev/),
# [3](https://docs.juliadiffeq.org/latest/solvers/ode_solve.html),
# [4](https://en.wikipedia.org/wiki/Displacement_(vector))
#
# Exercises: 
# - change the initial distribution of partices
# - increase the duration of the trajectories simulation
# - treat the non-periodic domain case by padding `u,v` with zeros 
# - replace `u,v` with your own two-dimensional flow fields 
#
# ![particles in random flow](https://github.com/JuliaClimate/IndividualDisplacements.jl/raw/master/examples/figs/RandomFlow.gif)

#nb # %% {"slideshow": {"slide_type": "slide"}, "cell_type": "markdown"}
# ## 1. Import Software

using IndividualDisplacements, DataFrames
p=dirname(pathof(IndividualDisplacements))
include(joinpath(p,"../examples/flow_fields.jl"));

#nb # %% {"slideshow": {"slide_type": "slide"}, "cell_type": "markdown"}
# ## 2. Flow Fields
#

nx=30
dx= π/nx
XC = dx*(collect(1:2*nx) .- 0.5)
YC = dx*(collect(1:nx) .- 0.5)

f(x, y) = sin(x) + cos(y) #streamfunction
ϕ = [f(x, y) for x in XC,y in YC] #streamfunction
uC = -[sin(y) for x in XC,y in YC] #dphi/dy at cell center
vC = -[cos(x) for x in XC,y in YC]; #-dphi/dx at cell center

# When starting in this fashion with collocated velocities in m/s units (`uC,vC` at the grid cell center), 
# one easily obtains the normalized, staggered C-grid velocities (`u,v`) as shown below. Any uC, vC
# computed as done above may contain contain both 
# [rotational and divergent](https://en.wikipedia.org/wiki/Helmholtz_decomposition)
# components -- if this were an important consideration, the MeshArrays.jl package
# tools can be used to e.g. ensure non-divergence. Normalization of velocities 
# by the grid scale is the convention in IndividualDisplacements.jl package

u=0.5*(circshift(uC, (1,0))+uC)/dx #staggered u converted to grid point units (m/s -> 1/s)
v=0.5*(circshift(vC, (0,1))+vC)/dx #staggered v converted to grid point units (m/s -> 1/s)

𝐹=𝐹_Array2D(u,u,v,v,[0.,10.])

# A convenient way to set up the flow fields using the MeshArrays.jl package (which 
# handles such staggered grids in general fashion) is to call the `convert_to_FlowFields`
# function with `u,v` arrays and time span as arguments.
#
# ```
# u,v,ϕ=random_flow_field()
# 𝐹=convert_to_FlowFields(u,v,100.0);
# ```

# The `u,v` arrays generated by `random_flow_field` can be replaced with any other pair provided by the user.
#
# A couple of important considerations, however:
#
# - `u,v` are staggered on a C-grid; by `-0.5` grid point in direction `1` for `u` (`2` for `v`)
#  from the grid cell center (0.5,0.5)
# - `u,v` here derive from streamfunction `ϕ`, defined at the corner point, which ensures that 
#  the resulting `u,v` is non-divergent, purely rotational, over the C-grid domain.
# 
# In brief:
#
# ```
# u=-(circshift(ϕ, (0,-1))-ϕ)
# v=(circshift(ϕ, (-1,0))-ϕ)
# ```

#nb # %% {"slideshow": {"slide_type": "slide"}, "cell_type": "markdown"}
# ## 3. Initialize Individuals
#
# For example, we can initialize 100 particles within a central subdomain as follows.

np,nq=size(u)
x=np*(0. .+ 1.0*rand(1000))
y=nq*(0. .+ 1.0*rand(1000))
a=ones(size(x)); #subdomain array index if using MeshArrays (just ones here)

# The following constructor function wraps everything in the `Individuals` data structure.

𝐼=Individuals(𝐹,x,y)

isa(𝐹,𝐹_MeshArray2D) ? 𝐼=Individuals(𝐹,x,y,a) : nothing

#nb # %% {"slideshow": {"slide_type": "slide"}, "cell_type": "markdown"}
# ## 3. Compute Trajectories
#
# The time period is `𝐼.𝑃.𝑇` by default, unless `∫!(𝐼,𝑇)` is called instead.

∫!(𝐼)

#nb # %% {"slideshow": {"slide_type": "slide"}, "cell_type": "markdown"}
# ## 4. Plot Results
#
# For example, generate a simple animation:

#md p=dirname(pathof(IndividualDisplacements))
#md include(joinpath(p,"../examples/recipes_plots.jl"));

#md 🔴_by_t = groupby(𝐼.🔴, :t)
#md anim = @animate for t in eachindex(🔴_by_t)
#md    phi_scatter(ϕ,🔴_by_t[t])
#md end

#md pth=tempdir()*"/"
#md gif(anim, pth*"RandomFlow.gif", fps = 10)
